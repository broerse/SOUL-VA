/** Processor [[main]] 

    '../examples/main.soul' modified to include all 'Processors' in 'VA::HighLevel' connected in series

    Notes:
    - Tests that all 'Processors' compile without errors
    - command: soul errors errors.soulpatch | echo
    - Only use with CLI is officially supported
*/

namespace main
{
    namespace FloatToSampleType
    {     
        VA::SampleType process (float x)
        {
            return VA::SampleType (x);
        }

        processor Processor()
        {
            input stream float in;
            output stream VA::SampleType out;
    
            void run()
            {   
                loop
                {
                    out << process (in);

                    advance();
                }
            }
        }
    }

    namespace SampleTypeToFloat
    {     
        float process (VA::SampleType x)
        {
            return float (x.sum()/VA::SampleType.size);
        }

        processor Processor()
        {
            input stream VA::SampleType in;
            output stream float out;
    
            void run()
            {   
                loop
                {
                    out << process (in);

                    advance();
                }
            }
        }
    }

    namespace va = VA::HighLevel;

    graph Processor [[main]]
    {
        input stream float in;
        output stream float out;

        input
        {
            //all input events from VA::HighLevel
            va1.stereoLinkIn va1_0;
            va1.nonlinearityIn va1_1;
            va1.volumeIn va1_2;

            va2.delayIn va2_0;
            va2.rateIn va2_1;
            va2.depthIn va2_2;
            va2.cutoffIn va2_3;
            va2.resonanceIn va2_4;
            va2.nonlinearityIn va2_5;
            va2.mixIn va2_6;
            va2.panIn va2_7;
        }

        let
        { 
            //all 'Processors' from VA::HighLevel
            va0 = va::Dummy::Processor;
            va1 = va::OnepoleC_Lan::Processor;
            va2 = va::ChorusLadderLpfS::Processor;

            up = FloatToSampleType::Processor;
            down = SampleTypeToFloat::Processor;
        }

        connection
        {       
            in -> up -> va0 -> va1.in;
            va1.out -> va2.in;
            va2.out -> down -> out;
        }
    }
}